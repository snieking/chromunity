import ft3_core: lib.ft3.core;
import noti: notifications;

operation send_vibe(name, descriptor_id: byte_array, receiver: name, amount: integer) {
	val user = get_and_charge_verified_user(name, descriptor_id);
	
	// Ensure receiver has a balance entity,
	// since old users won't have one by default.
	val receiving_user = user@{ .name == receiver.lower_case() };
	val their_balance = get_balance(receiving_user);
	if (their_balance == null) {
		create_balance(receiving_user, 0);
	}
	
	val asset = get_asset()!!;
	
	val input = ft3_core.xfer_input(
		account_id = user.account.id, 
		asset_id = asset.id, 
		auth_descriptor_id = descriptor_id, 
		amount, 
		extra = map<text, gtv>()
	);
	
	val output = ft3_core.xfer_output(
		account_id = receiving_user.account.id,
		asset_id = asset.id,
		amount,
		extra = map<text, gtv>()
	);
	
	ft3_core._transfer([ input ], [ output ]);
	
	var noun = "vibes";
	if (amount == 1)
		noun = "vibe";
		
	create noti.notification(
		id = op_context.transaction.tx_rid.to_hex(),
		receiving_user,
		trigger = "Received " + noun,
		content = "@" + user.display_name + " sent you " + amount + " " + noun,
		false,
		op_context.last_block_time
	);
}

query get_user_vibes(name) {
	val user = user@{ .name == name.lower_case() };
	val balance = get_balance(user);
	
	if (balance == null)
		return 0;
	else
		return balance.amount;
}

function reward_asset(user, amount: integer) {
	val account_balance = get_balance(user);
	
	val modified_amount = calculate_asset_with_modifier(amount);
	
	if (account_balance == null) {
		create_balance(user, modified_amount);
		return;
	}
		
	update account_balance(amount = account_balance.amount+modified_amount);
}

function get_balance(user): ft3_core.balance? {
	return ft3_core.balance@?{ 
		user.account,
		.asset.name == asset_name()
	};
}

function get_asset(): ft3_core.asset? {
	return ft3_core.asset@?{ .name == asset_name() };
}

function create_balance(user, amount: integer) {
	var asset = get_asset();
	
	if (asset == null) {
		asset = ft3_core.register_asset(asset_name(), chain_context.blockchain_rid);
	}
	
	create ft3_core.balance(user.account, asset, amount);
}

function asset_name(): text {
	return "VIBE";
}

function calculate_asset_with_modifier(amount: integer): integer {
	val block = op_context.block_height;
	
	return when {
		block < 10000000 -> amount * 10;
		block < 20000000 -> amount * 9;
		block < 30000000 -> amount * 8;
		block < 40000000 -> amount * 7;
		block < 50000000 -> amount * 6;
		block < 60000000 -> amount * 5;
		block < 70000000 -> amount * 4;
		block < 80000000 -> amount * 3;
		block < 90000000 -> amount * 2;
		else -> amount;
	};
}